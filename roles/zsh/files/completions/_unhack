#compdef unhack

_unhack() {
  _arguments \
    '(-h --help)'{-h,--help}'[Display help message]' \
    '(-n --dry-run)'{-n,--dry-run}'[Show what would be done without doing it]' \
    '1:branch:__unhack_worktree_branches'
}

__unhack_worktree_branches() {
  local repo_root
  repo_root=$(git rev-parse --show-toplevel 2>/dev/null) || return

  local -a branches
  local wt_path=""

  while IFS= read -r line; do
    case "$line" in
      "worktree "*)
        wt_path="${line#worktree }"
        ;;
      "branch refs/heads/"*)
        if [[ "$wt_path" = "$repo_root/.worktrees/"* ]]; then
          branches+=("${line#branch refs/heads/}")
        fi
        ;;
      "")
        wt_path=""
        ;;
    esac
  done < <(git worktree list --porcelain 2>/dev/null)

  _describe 'worktree branch' branches
}

_unhack "$@"
